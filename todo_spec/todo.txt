*** Settings ***
Library  Collections
Library  RequestsLibrary
Suite Teardown  Delete All Sessions

*** Variables ***
${TODO_URL}     https://juacompe-todo-app.firebaseio.com/todos.json

*** Test cases ***
Get All Todo List OK
    [Documentation]  ${TODO_URL} returns list of todo items.
    Given I Have Session Created
    ${response}=  When I Get All Todo List
    Then Status Should Be OK  ${response}

Get All Todo List Contains Learn Angular
    [Documentation]  Learn Angular (done) should be in the list.
    Given I Have Session Created
    ${response}=  When I Get All Todo List
    Then Content Should Contains Learn Angular  ${response}

Get All Todo List Contains Try Angular 
    [Documentation]  Try Angular (done) should be in the list.
    Given I Have Session Created
    ${response}=  When I Get All Todo List
    Then Content Should Contains Try Angular  ${response}

Get All Todo List Contains Master Angular 
    [Documentation]  Master Angular (not done) should be in the list.
    Given I Have Session Created
    ${response}=  When I Get All Todo List
    Then Content Should Contains Master Angular  ${response}

Get All Todo Contains Expected Items 
    [Documentation]  Can get each item directly by accessing its key.
    [Tags]  table
    [Template]  Get All Todo Should Contains Expected Items
    #--------------------------------------------------------------
    #        URL          |        Title          |      Done     |
    #--------------------------------------------------------------
        /todos/0.json           Learn Angular           ${true}
        /todos/1.json           Try Angular             ${true}
        /todos/2.json           Master Angular          ${false}


*** Keywords ***
Given I Have Session Created
    Create Session  todo firebase  https://juacompe-todo-app.firebaseio.com

I Get All Todo List
    ${resp}=    Get     todo firebase   /todos.json
    [return]    ${resp}

Status Should Be OK
    [Arguments]  ${resp}
    Should Be Equal As Strings  ${resp.status_code}     200

Content Should Contains Learn Angular
    [Arguments]  ${resp}
    Content Should Contains  ${resp}    Learn Angular   ${true}

Content Should Contains Try Angular
    [Arguments]  ${resp}
    Content Should Contains  ${resp}    Try Angular     ${true}

Content Should Contains Master Angular
    [Arguments]  ${resp}
    Content Should Contains  ${resp}    Master Angular  ${false}

Content Should Contains
    [Arguments]  ${resp}  ${title}  ${done}
    ${jsondata}=        To JSON                 ${resp.content}
    #${items}=          Get Dictionary Values   ${jsondata}
    ${expected item}    Create Dictionary       done=${done}    title=${title}
    Dictionary Should Contain Value             ${jsondata}     ${expected item}

Get All Todo Should Contains Expected Items
    [Arguments]  ${url}  ${title}  ${done}
    Given I Have Session Created
    ${response}=  When I Get An Item  ${url} 
    Item Should Be  ${response}  ${title}  ${done}

When I Get An Item
    [Arguments]  ${url}
    ${resp}=    Get     todo firebase   ${url} 
    [return]    ${resp}

Item Should Be
    [Arguments]  ${resp}  ${title}  ${done}
    ${item}=            To JSON            ${resp.content}
    ${expected item}    Create Dictionary  done=${done}    title=${title}
    Dictionaries Should Be Equal  ${expected item}    ${item}
