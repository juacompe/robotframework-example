*** Settings ***
Library  Collections
Library  RequestsLibrary
Suite Setup     Given I Have Session Created 
Suite Teardown  Delete All Sessions
Documentation   ${FIREBASE_URL}/todo.json returns list of todo items.

*** Variables ***
${FIREBASE_URL}     https://juacompe-todo-app.firebaseio.com

*** Test cases ***
Get All Todo List OK
    [Documentation]  Status code should be OK (200).
    ${response}=  When I Get All Todo List
    Then Status Should Be OK  ${response.status_code}

Get All Todo List Contains Learn Angular
    [Documentation]  *Learn Angular* (done) should be in the list.
    ${response}=  When I Get All Todo List
    Then Content Should Contains Learn Angular  ${response.content}

Get All Todo List Contains Try Angular 
    [Documentation]  *Try Angular* (done) should be in the list.
    ${response}=  When I Get All Todo List
    Then Content Should Contains Try Angular  ${response.content}

Get All Todo List Contains Master Angular 
    [Documentation]  *Master Angular* (not done) should be in the list.
    ${response}=  When I Get All Todo List
    Then Content Should Contains Master Angular  ${response.content}

Get All Todo Contains Expected Items 
    [Documentation]  Can get each item directly by accessing its key.
    [Tags]  table
    [Template]  Get All Todo Should Contains Expected Items
    #--------------------------------------------------------------
    #        URL          |        Title          |      Done     |
    #--------------------------------------------------------------
        /todos/0.json           Learn Angular           ${true}
        /todos/1.json           Try Angular             ${true}
        /todos/2.json           Master Angular          ${false}


*** Keywords ***
Given I Have Session Created
    Create Session  todo firebase  ${FIREBASE_URL} 

I Get All Todo List
    ${resp}=    Get     todo firebase   /todos.json
    [return]    ${resp}

Status Should Be OK
    [Arguments]  ${status code}
    Should Be Equal As Strings  ${status code}     200

Content Should Contains Learn Angular
    [Arguments]  ${content}
    Content Should Contains  ${content}    Learn Angular   ${true}

Content Should Contains Try Angular
    [Arguments]  ${content}
    Content Should Contains  ${content}    Try Angular     ${true}

Content Should Contains Master Angular
    [Arguments]  ${content}
    Content Should Contains  ${content}    Master Angular  ${false}

Content Should Contains
    [Arguments]  ${content}  ${title}  ${done}
    ${jsondata}=        To JSON                 ${content}
    #${items}=          Get Dictionary Values   ${jsondata}
    ${expected item}    Create Dictionary       done=${done}    title=${title}
    Dictionary Should Contain Value             ${jsondata}     ${expected item}

Get All Todo Should Contains Expected Items
    [Arguments]  ${url}  ${title}  ${done}
    ${response}=  When I Get An Item  ${url} 
    Item Should Be  ${response.content}  ${title}  ${done}

When I Get An Item
    [Arguments]  ${url}
    ${resp}=    Get     todo firebase   ${url} 
    [return]    ${resp}

Item Should Be
    [Arguments]  ${content}  ${title}  ${done}
    ${item}=            To JSON            ${content}
    ${expected item}    Create Dictionary  done=${done}    title=${title}
    Dictionaries Should Be Equal  ${expected item}    ${item}
